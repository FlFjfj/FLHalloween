"maingame" module

"control" useModule
"string" useModule
"opengl" useModule
"glutils" useModule
"program" useModule

"assets" useModule
"worldmap" useModule
"player" useModule
"light" useModule

WORLD_WIDTH: 1424.0r32;
WORLD_HEIGHT: 728.0r32;

MainGame: {

  map: WorldMap;
  batch: spriteBatch;
  camera: WORLD_WIDTH WORLD_HEIGHT orthographicCamera;
  startTime: Real32;
  lastMonsterTime: Real32;
  player: Player;
  monsters: Monster Array;
  gui: GUI;
  
  lightSys: LightSystem;
  
  init: [
    system.getTime @lastMonsterTime set
    @batch.init
    @map.init
    @player.init
    Monster.init
    @gui.init
  ] func;

  enterState: [
    1.0r32 0.0r32 0.0r32 0.0r32 glClearColor
  ] func;

  draw: [
    GL_COLOR_BUFFER_BIT glClear
    @camera.beforeDraw
    map.drawFloor
    player.draw
    monsters fieldCount [i monsters @ .draw] times
    map.drawObj
    sys: lightSys;
    gui.draw
  ] func;

  update: [
    time: system.getTime;
    time lastMonsterTime - 5.0r32 > [
      time @lastMonsterTime set
      Monster @monsters.pushBack
    ] when

    @map.update
    @player.update
    nMonsters: Monster Array;
    monsters fieldCount [
      i @monsters @ .update [
        i @monsters @ @nMonsters.pushBack
      ] when
    ] times

    nMonsters @monsters set
    @camera.update
    player.health 0.0r32 > not [
      GameState.GameOver changeState
    ] when
  ] func;

};
