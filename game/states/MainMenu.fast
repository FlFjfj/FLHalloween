"mainmenu" module

"control" useModule
"string" useModule
"opengl" useModule

"assets" useModule

WORLD_WIDTH: 1424.0r32;
WORLD_HEIGHT: 728.0r32;

MainMenu: {
  batch: spriteBatch;
  camera: WORLD_WIDTH WORLD_HEIGHT orthographicCamera;
  shader: GLuint;
  projLoc: GLint;
  modelLoc: GLint;

  init: [
    "Menu init" print LF print
    @batch.init
    MainMenuStatic.shaderVert MainMenuStatic.shaderFrag ( ("u_modelTrans" @modelLoc) ("u_projTrans" @projLoc) ) loadShader !shader
  ] func;

  draw: [
   # "Menu draw" print LF print
    @camera.beforeDraw
    GL_COLOR_BUFFER_BIT glClear
    shader glUseProgram
    camera.getCombined GL_FALSE Nat8 cast 1i32 projLoc glUniformMatrix4fv
    assets.menuImage 0.0r32 0.0r32 1424.0r32 728.0r32 0.0r32 modelLoc batch.draw
    0n32 glUseProgram
  ] func;

  update: [
   # "Menu update" print LF print
    @camera.update
    SPACE system.isKeyDown  [
      GameState.MainGame changeState
    ] when
  ] func;

  enterState: [
    1.0r32 0.0r32 0.0r32 0.0r32 glClearColor
  ] func;
};

MainMenuStatic: {
  shaderVert:
  "
#version 330 core

  layout (location = 0) in vec3 aPosition;
  layout (location = 1) in vec2 aCoord;

  uniform mat4 u_projTrans;
  uniform mat4 u_modelTrans;

  out vec2 vCoord;
  
  void main() {
    vCoord = aCoord;
    gl_Position = u_projTrans * u_modelTrans * vec4(aPosition, 1.0);
  }
  ";

  shaderFrag:
  "
#version 330 core

  precision mediump float;
  
  in vec2 vCoord;
  out vec4 color;
  
  uniform sampler2D u_Texture;
  
  void main() {
    color = texture(u_Texture, vCoord);
  }
  ";
};
