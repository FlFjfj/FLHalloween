"pumpkin" module

"control" useModule
"algebra" useModule
"opengl" useModule
"glutils" useModule
"glwrapper" useModule

PumpkinVars: {
  PUMPKIN_WIDTH: 50.0r32;
  PUMPKIN_HEIGHT: 50.0r32;
  PUMPKIN_SPEED: 600.0r32;

  shader: GLuint;
  timeLoc: GLint;
  projLoc: GLint;
  modelLoc: GLint;
  lightUsedLoc: GLint;
  lightPosLoc: GLint;
  lightRadLoc: GLint;
  lightColorLoc: GLint;
  damage: 0.251r32;
  textures: (2 [GLuint] times);
};

Pumpkin: [{
  position:direction: copy; copy;
  position_new: position copy;
  texture: 0i32;
  lamp: 0i32;

  @lightSys.reserveLight !lamp
  lamp -1i32 = neg
  [
    (0.7r32 0.7r32 0.0r32) lamp @lightSys.@color @ set
    100.0r32 lamp @lightSys.@radius @ set
    coords lamp @lightSys.@position @ set
  ] when

  0 direction @ 0.0r32 = ~
  [ 1i32 !texture ] when

  (0.0r32 0.0r32) direction =
  [ (0.0r32 -1.0r32) !direction ] when

  direction normalize !direction

  draw: [
    PumpkinVars.shader glUseProgram
    coords: position position_new position - frameTime * +;

    getTime PumpkinVars.timeLoc glUniform1f
    camera.getCombined GL_TRUE Nat8 cast 1i32 PumpkinVars.projLoc glUniformMatrix4fv
    lightSys.getColor    lightSys.MAXIMUM lightColorLoc glUniform3fv
    lightSys.getPosition lightSys.MAXIMUM lightPosLoc   glUniform2fv
    lightSys.getRadius   lightSys.MAXIMUM lightRadLoc   glUniform1fv
    lightSys.getUsed     lightSys.MAXIMUM lightUsedLoc  glUniform1fv

    texture PumpkinVars.textures @ coords unwind PumpkinVars.PUMPKIN_WIDTH PumpkinVars.PUMPKIN_HEIGHT 0.0r32 PumpkinVars.modelLoc batch.draw
    0n32 glUseProgram
    lamp -1 = not
    [
      coords lamp @lightSys.@position @ set
    ] when

  ] func;

  update: [
    newState: direction PumpkinVars.PUMPKIN_SPEED * FRAME_DURATION * position_new +;
    position_new copy !position
    newState     copy !position_new
    collided: FALSE;
    monsters fieldCount [
      monster: i @monsters @;
      monster.state1 position_new - len PumpkinVars.PUMPKIN_WIDTH 2.0r32 * < [
        monster.health PumpkinVars.damage - @monster.@health set
        TRUE !collided
      ] when
    ] times

    isOnWall? not collided not and
    isAlive:;
    isAlive not lamp -1 = not and
    [lamp @lightSys.freeLight] when
    isAlive
  ] func;

  isOnWall?: [
    coords: position_new;
    map: MainGame.map;
    result: FALSE;
    map.mapWidth [col: i;
      map.mapHeight [row: i;
        col row isWallTile? [
          tileCoords: col row map.staticMapCoordinates @ @;
          coords tileCoords - len map.R Real32 cast 35.0r32 + < result or !result
        ] when
      ] times
    ] times

    result
  ] func;

}] func;

PumpkinStaticShader: {
  shaderVert:
  "
  #version 330 core
  
  #define FRAME_COUNT 7.0
  #define FRAME_TIME 0.05
  
  layout (location = 0) in vec3 aPosition;
  layout (location = 1) in vec2 aCoord;

  uniform mat4 u_projTrans;
  uniform mat4 u_modelTrans;
  uniform float u_Time;
  
  out vec2 vCoord;
  out vec2 worldCoord;
  flat out float vFrame;
  
  void main() {
    vFrame = int((u_Time - int(u_Time)) * FRAME_COUNT);
    vCoord = aCoord;
    vec4 position = u_modelTrans * vec4(aPosition, 1.0);
    worldCoord = position.xy;
    gl_Position = u_projTrans * position;
  }
  ";

  shaderFrag:
  "
  #version 330 core

  #define FRAME_COUNT 7.0 
  
  precision mediump float;
  
  in vec2 vCoord;
  in vec2 worldCoord;
  flat in float vFrame;
  
  out vec4 color;
  
  uniform sampler2D u_Texture;
  uniform vec2  u_position[32];
  uniform vec3  u_color[32];
  uniform float u_radius[32];
  uniform float u_used[32];
  
  void main() {
    vec3 lightColor = vec3(0.0);
    for(int i = 0; i < 32; i++) {
      if(u_used[i] == 1.0){
        float len = clamp(1.0 - length(worldCoord - u_position[i]) / u_radius[i], 0.0, 1.0);
        lightColor = lightColor + u_color[i] * len * len;
      }
    }
  
    vec4 texColor = texture(u_Texture, vec2((vCoord.x + vFrame) / FRAME_COUNT, vCoord.y));
    color = vec4(mix(texColor.xyz * lightColor, texColor.xyz, 0.05), texColor.a);
  }
  ";
};
